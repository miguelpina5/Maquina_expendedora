#include <Thread.h>
#include <StaticThreadController.h>
#include <ThreadController.h>
#include <DHT11.h>
#include <avr/wdt.h>
#include <LiquidCrystal.h>

// States
#define START 0
#define WAIT_CLIENT 1
#define WEATHER_5SEC 2
#define CHOOSE_COFFEE 3
#define PREPARE_COFFEE 4
#define REMOVE_COFFEE 5
#define ADMIN 6

// Admin`s states
#define ADMIN_WEATHER 0
#define ADMIN_DISTANCE 1
#define ADMIN_COUNTER 2
#define ADMIN_MENU_PRICES 3
#define ADMIN_CHANGE_PRICES 4
#define ADMIN_MENU 5

#define ADMIN_TIME 5000
#define LOW_RESET_TIME 2000
#define HIGH_RESET_TIME 3000
#define METER 100 // 1m = 100cm
#define SEC_TO_MS 1000
#define DEBOUNCE 200

// LiquidCrystal
LiquidCrystal lcd(12, 13, 7, 6, 5, 4);

// Ultrasonic
const byte TRIGGER = A3;
const byte ECHO = A2;

// Joystick
const byte SW = 2;
const byte VRX = A0;
const byte VRY = A1;
volatile bool joystick_pressed = false;
volatile unsigned long last_interrupt_time_joystick = 0;
bool pass_for_center = true;

// Leds
const byte LED_RED = 11;
const byte LED_GREEN = 10;

// DHT
const byte DHT = 8;
DHT11 dht11(DHT);

// Button
const byte BUTTON = 3;
volatile bool button_pressed = false;
volatile bool button_rissing = false;
volatile unsigned long start_time_button = 0;
volatile unsigned long last_time_button = 0;
volatile unsigned long last_interrupt_time_button = 0;

// Threads
ThreadController controller = ThreadController();
Thread thread_start = Thread();
Thread thread_weather = Thread();
Thread thread_remove_coffee = Thread();

// States
byte state = START;
byte admin_state = ADMIN_MENU;
bool state_changed = false;

// Other variables
bool waiting = false;
float previous_ms, t_start_prepare;
byte waited_seconds = 0;
byte time_to_prepare = 0;
int index_product = 0;
int index_menu = 0;
int led_brightness = 0;
bool menu_to_choose = false;
float distance, init_price;
unsigned long counter;
byte RANDOM = A5;

// Arrays to show on LCD
String products[5] = {"Cafe Solo", "Cafe Cortado", "Cafe Doble", "Cafe Premium", "Chocolate"};
float prices[5] = {1.00, 1.10, 1.25, 1.50, 2.00};
String menu_to_choose_1[4] = {"   Ver", "   Ver", "   Ver", "Modificar"};
String menu_to_choose_2[4] = {"  temperatura   ", "distancia sensor", "    contador", "    precios"};

// Declare the functions
void press_joystick();
void press_button();
bool wait_time(int seconds);
void change_state(int new_state);
void change_admin_state(int new_state);
void check_time_button();
int check_joystick_x();
int check_joystick_y();
float look_distance();
void show_temp_hum();
void wait_client();
void choose_coffee();
void prepare_coffee();
void callback_thread_start();
void callback_thread_weather();
void callback_thread_remove_coffee();
void admin_menu();
void change_prices();
void admin();

void setup() {

  Serial.begin(9600);
  wdt_disable();
  wdt_enable(WDTO_8S);

  lcd.begin(16,2);
  lcd.clear();
  
  pinMode(TRIGGER, OUTPUT);
  pinMode(ECHO, INPUT);
  pinMode(SW, INPUT_PULLUP);

  pinMode(LED_GREEN, OUTPUT);
  pinMode(LED_RED, OUTPUT);
  pinMode(BUTTON, INPUT);

  // Interruptions
  attachInterrupt(digitalPinToInterrupt(SW), press_joystick, FALLING);
  attachInterrupt(digitalPinToInterrupt(BUTTON), press_button, CHANGE);

  // Thread_start to blinker led for 1 sec
  thread_start.enabled = true;
  thread_start.setInterval(SEC_TO_MS);
  thread_start.onRun(callback_thread_start);
  controller.add(&thread_start);

  // Thread_weather to show it for 5 sec
  thread_weather.enabled = false;
  thread_weather.setInterval(5*SEC_TO_MS);
  thread_weather.onRun(callback_thread_weather);
  controller.add(&thread_weather);

  // Thread_remove_coffee to show it for 3 sec
  thread_remove_coffee.enabled = false;
  thread_remove_coffee.setInterval(3*SEC_TO_MS);
  thread_remove_coffee.onRun(callback_thread_remove_coffee);
  controller.add(&thread_remove_coffee);

  randomSeed(analogRead(RANDOM));
}

void loop() {

  controller.run();
  check_time_button();

  switch(state) {
    case START:
      lcd.setCursor(0, 0);
      lcd.print("CARGANDO...");
      break;
    
    case WAIT_CLIENT:
      wait_client();
      break;

    case WEATHER_5SEC:
      show_temp_hum();
      break;

    case CHOOSE_COFFEE:
      choose_coffee();
      break;

    case PREPARE_COFFEE:
      prepare_coffee();
      break;

    case REMOVE_COFFEE:
      digitalWrite(LED_RED, HIGH);
      lcd.setCursor(5, 0);
      lcd.print("RETIRE");
      lcd.setCursor(5, 1);
      lcd.print("BEBIDA");
      break;

    case ADMIN:
      admin();
      break;
  }

  wdt_reset();
}

// Fuction for joystick interruption
void press_joystick() {
  unsigned long interrupt_time = millis();

  if (interrupt_time - last_interrupt_time_joystick > DEBOUNCE) {
    if (state == CHOOSE_COFFEE)
      joystick_pressed = true;
    
    if (state == ADMIN) {
      if (admin_state == ADMIN_MENU || admin_state == ADMIN_MENU_PRICES || admin_state == ADMIN_CHANGE_PRICES)
        joystick_pressed = true;
    }
  }

  last_interrupt_time_joystick = interrupt_time;
}

// Fuction for button interruption
void press_button() {
  unsigned long interrupt_time = millis();

  if (interrupt_time - last_interrupt_time_button > DEBOUNCE) {
    if (button_pressed == false) {
      start_time_button = millis();
      button_pressed = true;
      button_rissing = false;

    } else {
      last_time_button = millis();
      button_pressed = false;
      button_rissing = true;
    }
  }
  last_interrupt_time_button = interrupt_time;
}

// Function to wait seconds to pass by argument
bool wait_time(int seconds) {

  if (!waiting) {
    previous_ms = millis();  // registra el inicio
    waiting = true;
    return false;
  }

  if (millis() - previous_ms >= (seconds * SEC_TO_MS)) {
    waiting = false;         // reinicia para la próxima vez que se llame
    return true;             // ya pasó 1 segundo
  }
  return false;              // todavía no ha pasado 1 segundo
}


void change_state(int new_state) {

  thread_remove_coffee.enabled = false;
  thread_start.enabled = false;
  thread_weather.enabled = false;
  menu_to_choose = false;
  index_product = 0;
  
  switch(new_state) {
    case WEATHER_5SEC:
      thread_weather.enabled = true;
      break;

    case CHOOSE_COFFEE:
      menu_to_choose = true;
      break;

    case ADMIN:
      menu_to_choose = true;
      admin_state = ADMIN_MENU;
      break;

    case REMOVE_COFFEE:
      thread_remove_coffee.enabled = true;
      break;
  }

  lcd.clear();
  digitalWrite(LED_GREEN, LOW);
  digitalWrite(LED_RED, LOW);
  waited_seconds = 0;
  state = new_state;
}

void change_admin_state(int new_state) {

  menu_to_choose = false;

  if (new_state == ADMIN_MENU || new_state == ADMIN_MENU_PRICES || new_state == ADMIN_CHANGE_PRICES)
    menu_to_choose = true;

  lcd.clear();
  admin_state = new_state;
}

// Check the time than button be pressed and change state depends of that
void check_time_button() {

  if (button_pressed && !state_changed) {
    unsigned long hold_time = millis() - start_time_button;
    Serial.println(hold_time);
    if (hold_time >= ADMIN_TIME) {
      if (state == ADMIN)
        change_state(WEATHER_5SEC);
      else
        change_state(ADMIN);
      state_changed = true;
    }
  }

  if (button_rissing) {
    if (state == WEATHER_5SEC || state == CHOOSE_COFFEE || state == PREPARE_COFFEE || state == REMOVE_COFFEE) {
      float time_final = last_time_button - start_time_button;

      if (time_final >= LOW_RESET_TIME && time_final <= HIGH_RESET_TIME)
        change_state(WEATHER_5SEC);
    }

    start_time_button = 0;
    last_time_button = 0;
    button_pressed = false;
    button_rissing = false;
    state_changed = false;
  }
}

int check_joystick_x() {
  int x_value = analogRead(VRX);
  int y_value = analogRead(VRY);
  int index_x = 0;

  if (pass_for_center) {
    if (x_value > 900) {
      index_x ++;
      pass_for_center = false;
      lcd.clear();
    }

    if (x_value < 100) {
      index_x --;
      pass_for_center = false;
      lcd.clear();
    }

  } else {
    // Check if joystick pass for the middle to evite incorrect functionality
    if (x_value > 300 && x_value < 700 && y_value > 300 && y_value < 700)
      pass_for_center = true;
  }
  return index_x;
}

int check_joystick_y() {
  int x_value = analogRead(VRX);
  int y_value = analogRead(VRY);
  int index_y = 0;

  if (pass_for_center) {
    if (y_value > 800) {
      index_y ++;
      pass_for_center = false;
      lcd.clear();
    }

  } else {
    if (x_value > 300 && x_value < 700 && y_value > 300 && y_value < 700)
      pass_for_center = true;
  }
  return index_y;
}

float look_distance() {
  digitalWrite(TRIGGER, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIGGER, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIGGER, LOW);

  long duration = pulseIn(ECHO, HIGH);
  float dist = duration * 0.034 / 2;

  Serial.print("Distancia: ");
  Serial.print(dist);
  Serial.println(" cm");

  return dist;
}

void show_temp_hum() {


  int temperature = 0;
  int humidity = 0;
  int result = dht11.readTemperatureHumidity(temperature, humidity);

  if (result == 0) {
    lcd.setCursor(0, 0);
    lcd.print("Temp: " );
    lcd.print(temperature);
    lcd.print(" C");
    
    lcd.setCursor(0, 1);
    lcd.print("Hum : " );
    lcd.print(humidity);
    lcd.print(" %" );
  }
}

void wait_client() {

  distance = look_distance();

  if (distance >= METER) {
    lcd.setCursor(3, 0);
    lcd.print("ESPERANDO");
    lcd.setCursor(4, 1);
    lcd.print("CLIENTE");

  } else {
    change_state(WEATHER_5SEC);
    thread_weather.enabled = true;
  }
}

void choose_coffee() {

  int index_x = check_joystick_x();
  index_product += index_x;

  if (index_product < 0)
    index_product = 4;
  if (index_product > 4)
    index_product = 0;

  // Show the product on LCD
  lcd.setCursor(0, 0);
  lcd.print(products[index_product]);
  lcd.setCursor(11, 1);
  lcd.print(prices[index_product]);
  lcd.print("$");

  if (joystick_pressed) {
    if (state == CHOOSE_COFFEE) {
      change_state(PREPARE_COFFEE);
      time_to_prepare = random(4, 9); // 4 included, 9 not
      t_start_prepare = millis();
      index_product = 0;

    } else if (state == ADMIN) {
      if (admin_state == ADMIN_MENU_PRICES) {
        change_admin_state(ADMIN_CHANGE_PRICES);
        init_price = prices[index_product];

      }
    }
    joystick_pressed = false;
  }
}

void prepare_coffee() {

  if (wait_time(time_to_prepare))
    change_state(REMOVE_COFFEE);

  else {
    // Turn on the led increasingly
    float total_ms = time_to_prepare * SEC_TO_MS;
    float elapsed_ms = (millis() - t_start_prepare);
    float ratio = min(1.0, elapsed_ms / total_ms);

    int brightness = ratio * 255.0;

    analogWrite(LED_RED, brightness);

    lcd.setCursor(3, 0);
    lcd.print("Preparando");
    lcd.setCursor(4, 1);
    lcd.print("Cafe...");
  }
}

void callback_thread_start() {

  digitalWrite(LED_GREEN, !digitalRead(LED_GREEN));
  waited_seconds++;
  
  if (waited_seconds >= 6) {
    thread_start.enabled = false;
    change_state(WAIT_CLIENT);
  }
}

void callback_thread_weather() {

  if (waited_seconds == 5)
    change_state(CHOOSE_COFFEE);

  else
    waited_seconds = 5;
}

void callback_thread_remove_coffee() {

  if (waited_seconds == 3)
    change_state(WAIT_CLIENT);

  else 
    waited_seconds = 3;
}

void admin_menu() {
  int index_x = check_joystick_x();
  index_menu += index_x;

  if (index_menu < 0)
    index_menu = 3;
  if (index_menu > 3)
    index_menu = 0;

  lcd.setCursor(3, 0);
  lcd.print(menu_to_choose_1[index_menu]);
  lcd.setCursor(0, 1);
  lcd.print(menu_to_choose_2[index_menu]);

  if (joystick_pressed) {
    change_admin_state(index_menu);
    joystick_pressed = false;
  }
}

void change_prices() {
  int index_x = check_joystick_x();

  if (index_x == -1)
    init_price += 0.05;
  if (index_x == 1)
    init_price -= 0.05;

  if (init_price < 0.00)
    init_price = 0.00;

  lcd.setCursor(0, 0);
  lcd.print(products[index_product]);
  lcd.setCursor(8, 1);
  lcd.print("  ~");
  lcd.print(init_price);
  lcd.print("$");

  if (joystick_pressed) {
    prices[index_product] = init_price;
    change_admin_state(ADMIN_MENU_PRICES);
    joystick_pressed = false;
  }
}

void admin() {

  digitalWrite(LED_GREEN, HIGH);
  digitalWrite(LED_RED, HIGH);
  int index_y = check_joystick_y();

  switch(admin_state) {
    case ADMIN_WEATHER:
      show_temp_hum();
      if (index_y == 1)
        change_admin_state(ADMIN_MENU);
      break;

    case ADMIN_DISTANCE:
      distance = look_distance();
      lcd.setCursor(3, 0);
      lcd.print("Distancia:" );
      lcd.setCursor(4, 1);
      lcd.print(distance);
      lcd.print(" cm       ");
      if (index_y == 1)
        change_admin_state(ADMIN_MENU);
      break;

    case ADMIN_COUNTER:
      counter = millis() / 1000;
      lcd.setCursor(3, 0);
      lcd.print("Contador:");
      lcd.setCursor(5, 1);
      lcd.print(counter);
      lcd.print(" s        ");
      if (index_y == 1)
        change_admin_state(ADMIN_MENU);
      break;

    case ADMIN_MENU_PRICES:
      choose_coffee();
      if (index_y == 1) {
        index_product = 0;
        change_admin_state(ADMIN_MENU);
      }
      break;

    case ADMIN_CHANGE_PRICES:
      change_prices();
      if (index_y == 1)
        change_admin_state(ADMIN_MENU_PRICES);
      break;

    case ADMIN_MENU:
      admin_menu();
      break;
  }
}
